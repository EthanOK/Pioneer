
>- 原文链接：https://medium.com/rareskills/delegatecall-the-detailed-and-animated-guide-30621d6b2c81
>- 译者：[AI翻译官](https://learnblockchain.cn/people/19584)，校对：[翻译小组](https://learnblockchain.cn/people/412)
>- 本文永久链接：[learnblockchain.cn/article…](https://learnblockchain.cn/article/8827)
    
# Delegatecall: The Detailed and Animated Guide

本文详细解释了 delegatecall 的工作原理。以太坊虚拟机（EVM）提供了四个用于在合约之间进行调用的操作码：

*   `**CALL (F1)**`
*   `**CALLCODE (F2)**`
*   `**STATICCALL (FA)**`
*   和 `**DELEGATECALL (F4)**`

值得注意的是，自 Solidity v5 以来，`**CALLCODE**`操作码已被弃用，并由`**DELEGATECALL**`取代。这些操作码在 Solidity 中有直接的实现，可以作为类型为`address`的变量的方法执行。

为了更好地理解 delegatecall 的工作原理，让我们首先回顾一下`**CALL**`操作码的功能。

# CALL

为了演示 call，请考虑以下合约：

contract Called {  
  uint public number;  
  
function increment() public {  
    number++;  
  }  
}

从另一个合约中执行 increment()函数的最直接方法是利用 Called 合约接口。在这个示例中，我们可以通过一个简单的语句 called.increment()来执行该函数，其中 called 是 Called 的地址。但也可以使用低级别的 call 来调用 increment()，如下合约所示：

contract Caller {  
  
address constant public calledAddress = 0xd9145CCE52D386f254917e481eB44e9943F39138; // Called 的地址  
  
function callIncrement() public {  
        calledAddress.call(abi.encodeWithSignature("increment()"));  
    }  
}

每个类型为 address 的变量，例如 calledAddress 变量，都有一个名为 call 的方法。该方法期望参数是要在交易中执行的输入数据，即 ABI 编码的 calldata。在上述情况下，输入数据必须对应于`**increment()**`函数的签名，函数选择器为`0xd09de08a`。我们使用 abi.encodeWithSignature 方法从函数定义生成此签名。

如果在`Caller`合约中执行`callIncrement`函数，你会发现`Called`中的状态变量`number`将增加 1。`call`方法**不会验证目标地址是否实际对应于现有合约，也不会验证它是否包含指定的函数**。

视频中展示了 call 交易的可视化过程：

# Call returns a tuple

`call`方法返回一个包含两个值的元组。第一个值是一个布尔值，指示交易的成功或失败。第二个值是类型为 bytes 的，包含由`call`执行的函数的返回值，经过 ABI 编码（如果有）。

为了检索`call`的返回值，我们可以如下修改`callIncrement`函数：

function callIncrement() public {  
	(bool success, bytes memory data) = called.call(  
		abi.encodeWithSignature("increment()")  
	);      
}

`call`方法从不回滚。如果交易不`成功`，success 将为 false，程序员需要相应地处理。

# Handling Call Failures

让我们修改上述合约以包含对不存在函数的另一个调用，如下所示。

contract Caller {  
    address public constant calledAddress = 0xd9145CCE52D386f254917e481eB44e9943F39138;      
  
function callIncrement() public {  
        (bool success, bytes memory data) = called.call(  
			abi.encodeWithSignature("increment()")  
		);          
  
if (!success) {  
            revert("Something went wrong");  
        }  
    }  
        // 调用一个不存在的函数  
    function callWrong() public {  
        (bool success, bytes memory data) = called.call(  
			abi.encodeWithSignature("thisFunctionDoesNotExist()")  
		);          
  
if (!success) {  
            revert("Something went wrong");  
        }  
    }  
}

我故意创建了两个函数：一个具有正确的 increment 函数签名，另一个具有无效签名。第一个函数将返回`true`表示`success`，而第二个将返回`false`。返回的布尔值被显式处理，如果`success`为 false，交易将回滚。

我们必须小心跟踪调用是否成功，我们稍后会重新讨论这个问题。

# What the EVM does under the hood

`increment`函数的目的是增加名为`number`的状态变量。由于 EVM 不了解状态变量，而是**在存储槽上操作**，所以函数实际上是增加存储的第一个槽中的值，即槽 0。此操作发生在`Called`合约的存储中。

![](https://img.learnblockchain.cn/attachments/migrate/1721878509338)

回顾如何使用`call`方法将帮助我们形成关于如何使用`**delegatecall**`的概念。

# DELEGATECALL

**一个合约对目标智能合约进行 delegatecall 时，会在自己的环境中执行目标合约的逻辑。**

一种思维模型是它复制目标智能合约的代码并自行运行该代码。目标智能合约通常被称为“实现合约”。

与`call`一样，`delegatecall`也将要由目标合约执行的输入数据作为参数。

以下是`Called`合约的代码，对应于上面的动画，它在`Caller`的环境中运行：

contract Called {  
  uint public number;    
  
function increment() public {  
    number++;  
  }  
}

以及`Caller`的代码

contract Caller {  
    uint public number;      
  
function callIncrement(address \_calledAddress) public {  
		\_calledAddress.delegatecall(  
			abi.encodeWithSignature("increment()")  
		);  
    }  
}

这个`**delegatecall**`将执行`increment`函数；然而，执行将发生一个关键的区别。`Caller`合约的存储将被修改，**而不是**`Called`的存储。就像`Caller`合约借用了`Called`的代码在自己的上下文中执行。

下图进一步说明了`**delegatecall**`如何修改`Caller`的存储而不是`Called`的存储。

![](https://img.learnblockchain.cn/attachments/migrate/1721878509341)

下图说明了使用`**call**`和`**delegatecall**`执行 increment 函数的区别。

![](https://img.learnblockchain.cn/attachments/migrate/1721878509345)

# Storage slot collision

发出`**delegatecall**`的合约必须非常小心地预测其存储槽将被修改。前面的示例之所以能完美运行，是因为`Caller`没有使用槽 0 中的状态变量。使用`**delegatecall**`时常见的错误是忘记这一事实。让我们看一个例子。

contract Called {  
  uint public number;    
  
function increment() public {  
    number++;  
  }  
}  
  
contract Caller {  
        // 这里有一个新的存储变量  
    address public calledAddress = 0xd9145CCE52D386f254917e481eB44e9943F39138;      
  
uint public myNumber;      
  
function callIncrement() public {          
		called.delegatecall(  
			abi.encodeWithSignature("increment()")  
		);  
    }  
  
}

请注意，在上述更新的合约中，槽`**0**`的内容是`Called`合约的地址，而`myNumber`变量现在存储在槽`**1**`中。

如果部署提供的合约并执行`callIncrement`函数，`Caller`存储的槽 0 将被增加，但`calledAddress`变量在那里，而不是`myNumber`变量。以下视频展示了这个 bug：

让我们在下面说明发生了什么。

![](https://img.learnblockchain.cn/attachments/migrate/1721878509383)

因此，在使用`**delegatecall**`时必须谨慎，因为它可能会无意中破坏我们的合约。在上面的例子中，程序员可能并不打算通过`callIncrement`函数更改`calledAddress`变量。

让我们通过将状态变量`myNumber`移动到槽 0 来对`Caller`进行一个小改动。

```solidity
contract Caller {      
  
uint public myNumber;      
  
address public calledAddress = 0xd9145CCE52D386f254917e481eB44e9943F39138;      
  
function callIncrement() public {  
        called.delegatecall(  
			abi.encodeWithSignature("increment()")  
		);  
    }  
}
```

现在，当执行`callIncrement`函数时，`myNumber`变量将被递增，因为这是`increment`函数的目的。我故意选择了与`Called`中的变量不同的变量名，以证明**变量名并不重要；重要的是它在哪个槽中**。对齐两个合约的状态变量对于 delegatecall 的正常运行至关重要。

# 将实现与数据解耦

`**delegatecall**`最重要的用途之一是将存储数据的合约（如本例中的`Caller`）与执行逻辑所在的合约（如`Called`）解耦。因此，如果希望更改执行逻辑，只需用另一个合约替换`Called`并更新实现合约的引用，而无需触及存储。`Caller`不再受限于它拥有的函数，它可以从其他合约中 delegatecall 所需的函数。

如果需要更改执行逻辑，例如将`myNumber`的值减去 1 个单位而不是增加，可以创建一个新的实现合约，如下所示。

```solidity
contract NewCalled {      
  
uint public number;      
  
function increment() public {  
        number = number - 1;  
    }  
}
```

不幸的是，不可能更改将被调用的函数的名称，因为这样做会改变其签名。

在创建新的实现合约`NewCalled`后，只需部署这个新合约并更改`Caller`中的`calledAddress`状态变量。当然，Caller 需要有一个机制来更改它发出`delegateCall`的地址，我们没有包括这个机制以保持代码简洁。

我们已经成功地修改了 Caller 合约使用的业务逻辑。**将数据与执行逻辑分离使我们能够在 Solidity 中创建可升级的智能合约。**

![](https://img.learnblockchain.cn/attachments/migrate/1721878509489)

在上图中，左边的合约处理数据和逻辑。右边，顶部的合约持有数据，但更新数据的机制在逻辑合约中。要更新数据，需要对逻辑合约进行 delegatecall。

# 处理 delegatecall 的返回值

就像`**call**`一样，`**delegatecall**`也返回一个包含两个值的元组：一个表示执行成功的布尔值和通过`**delegatecall**`执行的函数的返回值（以字节形式）。为了了解如何处理这个返回值，让我们写一个新的例子。

```solidity
contract Called {  
    function calculateDiscountPrice(uint256 amount, uint256 discountRate) public pure returns (uint) {  
        return amount - (amount * discountRate)/100;  
    }  
}  
  
contract Caller {  
    uint public price = 200;  
    uint public discountRate = 10;  
    address public called;  
    function setCalled(address _called) public {  
        called = _called;  
    }  
  
    function setDiscountPrice() public  {  
        (bool success, bytes memory data) = called.delegatecall(  
			abi.encodeWithSignature(  
				"calculateDiscountPrice(uint256,uint256)",   
				price,   
			discountRate)  
		);  
  
        if (success) {  
            uint newPrice = abi.decode(data, (uint256));  
            price = newPrice;  
        }  
    }  
}
```

`Called`合约包含计算折扣价格的逻辑。我们通过执行`calculateDiscountPrice`函数来利用这个逻辑。这个函数返回一个值，我们必须使用`**abi.decode**`进行解码。在根据这个返回值做出任何决定之前，必须检查函数是否成功执行，否则我们可能会尝试解析一个不存在的返回值或最终解析一个回退原因字符串。

# 当 call 和 delegatecall 返回 false 时

一个关键点是理解成功值何时为`true`或`false`。**本质上，这取决于被执行的函数是否会回退。** 有三种方式可以导致执行回退：

*   如果遇到 REVERT 操作码，
*   如果耗尽 gas，
*   如果尝试某些禁止的操作，例如除以零。

如果通过`delegatecall`（或`**call**`）执行的函数遇到任何这些情况，它将回退，并且`delegatecall`的返回值将为 false。

一个经常困扰开发者的问题是为什么对不存在的合约进行`delegatecall`不会回退并且仍然报告执行成功。根据我们所说的，一个空地址永远不会满足回退的三个条件之一，所以它永远不会回退。

# 另一个存储变量陷阱的例子

让我们对上面的代码稍作修改，以给出另一个与存储布局相关的 bug 的例子。

`Caller`合约仍然通过`**delegatecall**`调用一个实现合约，但现在`Called`合约从状态变量中读取一个值。这看起来像是一个小改动，但实际上会导致灾难。你能找出为什么吗？

```solidity
contract Called {  
    uint public discountRate = 20;  
      
    function calculateDiscountPrice(uint256 amount) public pure returns (uint) {  
        return amount - (amount * discountRate)/100;  
    }  
}  
  
contract Caller {  
    uint public price = 200;  
    address public called;  
    function setCalled(address _called) public {  
        called = _called;  
    }  
    function setDiscount() public  {  
        (bool success, bytes memory data) =called.delegatecall(  
			abi.encodeWithSignature(  
				"calculateDiscountPrice(uint256)",   
				price  
			)  
		);  
  
        if (success) {  
            uint newPrice = abi.decode(data, (uint256));  
            price = newPrice;  
        }  
    }  
}
```

问题在于`calculateDiscountPrice`正在读取一个状态变量，特别是槽 0 中的那个。记住，在`**delegatecall**`中，函数是在调用合约的存储中执行的。换句话说，你可能认为你在使用`Called`合约中的`discountRate`变量来计算新的`price`，但实际上你在使用`Caller`合约中的 price 变量！存储变量`Called.discountRate`和`Called.price`占据槽 0。

你将获得 200%的折扣，这相当可观（并且会导致函数回退，因为新计算的价格将变为负数，这对于**uint**类型变量是不允许的）。

# delegatecall 中的不可变和常量变量：一个 bug 故事另一个与 `delegatecall` 相关的棘手问题出现在涉及不可变或常量变量时。让我们来看一个许多经验丰富的 Solidity 程序员容易误解的例子：

```solidity
contract Caller {  
    uint256 private immutable a = 3;  
    function getValueDelegate(address called) public pure returns (uint256) {  
        (bool success, bytes memory data) = B.delegatecall(  
            abi.encodewithSignature("getValue()"));  
        return abi.decode(data, (uint256)); // is this 3 or 2?  
    }  
}  
  
contract Called {  
    uint256 private immutable a = 2;  
  
    function getValue() public pure returns (uint256) {  
        return a;  
    }  
}
```

问题是：执行 `getValueDelegate` 时，返回值是 2 还是 3？让我们来推理一下。

* `getValueDelegate` 函数执行 `getValue` 函数，该函数应该返回槽 0 中对应的状态变量的值。
* 由于是 delegatecall，我们应该检查调用合约中的槽，而不是被调用合约中的槽。
* `Caller` 中变量 `a` 的值是 3，所以响应必须是 3。搞定了。

令人惊讶的是，正确答案是 2。为什么？！

**不可变或常量状态变量不是真正的状态变量：它们不占用槽**。当我们声明不可变变量时，它们的值被硬编码在合约字节码中，该字节码在 delegatecall 期间执行。因此，`getValue` 函数返回硬编码的值 2。

# msg.sender, msg.value 和 address(this)

如果我们在 `Called` 合约中使用 `**msg.sender**`、`**msg.value**` 和 `**address(this)**`，所有这些值将对应于 Caller 合约的 `**msg.sender**`、`**msg.value**` 和 `**address(this)**` 值。让我们记住 delegatecall 的操作方式：一切都在调用合约的上下文中发生。**实现合约仅提供要执行的字节码，仅此而已。**

![](https://img.learnblockchain.cn/attachments/migrate/1721878510606)

让我们在一个例子中应用这个概念。考虑以下代码：

```solidity
contract Called {  
    function getInfo() public payable returns (address, uint, address) {  
        return (msg.sender, msg.value, address(this));  
    }  
}  
  
contract Caller {  
    function getDelegatedInfo(address _called) public payable returns (address, uint, address) {  
        (bool success, bytes memory data) = _called.delegatecall(  
            abi.encodeWithSignature("getInfo()")  
        );  
        return abi.decode(data, (address, uint, address));  
    }  
}
```

在 `Called` 合约中，我使用了 `**msg.sender**`、`**msg.value**` 和 `**address(this)**`，并在 getInfo 函数中返回这些值。在下图中，使用 **Remix** 执行 `getDelegateInfo`，显示了返回的值。

* `**msg.sender**` 对应于执行交易的账户，具体来说是第一个 Remix 默认账户，即 `0x5B38Da6a701c568545dCfcB03FcB875f56beddC4`。
* `**msg.value**` 反映了在原始交易中发送的 1 个以太币的值。
* `**address(this)**` 是 Caller 合约的地址，如图左侧所示，而不是 Called 合约的地址。

![](https://img.learnblockchain.cn/attachments/migrate/1721878510629)

在 Remix 中，我们显示了 msg.sender (0)、msg.value (1) 和 address(this) (2) 的日志值。

# msg.data 和 delegatecall 中的输入数据

`msg.data` 属性返回正在执行的上下文的 calldata。当 `msg.data` 在由 EOA 直接通过交易执行的函数中被调用时，`msg.data` 代表交易的输入数据。

当我们执行 call 或 delegatecall 时，我们将作为参数指定将在实现合约中执行的输入数据。因此，原始 calldata 与由 `**delegatecall**` 创建的子上下文中的 calldata 不同，因此 `msg.data` 也会不同。

![](https://img.learnblockchain.cn/attachments/migrate/1721878510699)

下面的代码将用于演示这一点。

```solidity
contract Called {  
    function returnMsgData() public pure returns (bytes memory) {  
        return msg.data;  
    }  
}  
  
contract Caller {  
    function delegateMsgData(address _called) public returns (bytes memory data) {  
        (, data) = _called.delegatecall(  
            abi.encodeWithSignature("returnMsgData()")  
        );  
    }  
}
```

原始交易执行 `delegateMsgData` 函数，该函数需要一个地址类型的参数。���此，输入数据将包含函数签名和一个地址，ABI 编码。

`delegateMsgData` 函数反过来 delegatecalls `returnMsgData` 函数。为此，传递给运行时的 calldata 必须包含 `returnMsgData` 的签名。因此，`returnMsgData` 内部的 `msg.data` 的值是其自身的签名，即 `0x0b1c837f`。

在下图中，我们可以看到 `returnMsgData` 的返回值是其自身的签名，ABI 编码。

![](https://img.learnblockchain.cn/attachments/migrate/1721878510772)

解码输出是 `returnMsgData` 函数的签名，ABI 编码为字节。

# Codesize 作为反例

我们提到可以通过借用实现合约的字节码并在调用合约中执行它来构思 delegatecall。有一个例外，即 `CODESIZE` 操作码。

假设一个智能合约的字节码中有 `CODESIZE`，`CODESIZE` 返回该合约的大小。Codesize 不会在 delegatecall 期间返回调用者代码的大小——它返回被 delegatecalled 的代码的大小。

为了演示这一特性，我们提供了下面的代码。在 Solidity 中，`CODESIZE` 可以通过 `codesize()` 函数在汇编中执行。我们有两个实现合约，`CalledA` 和 `CalledB`，它们仅在局部变量（`ContractB` 中的 `unused`——该变量在 `ContractA` 中不存在）上有所不同，目的是确保合约大小不同。这些合约通过 Caller 合约的 `getSizes` 函数使用 `**delegatecall**` 调用。

```solidity
// codesize 1103  
contract Caller {  
    function getSizes(address _calledA, address _calledB) public returns (uint sizeA, uint sizeB) {  
        (, bytes memory dataA) = _calledA.delegatecall(  
            abi.encodeWithSignature("getCodeSize()")  
        );  
        (, bytes memory dataB) = _calledB.delegatecall(  
            abi.encodeWithSignature("getCodeSize()")  
        );  
        sizeA = abi.decode(dataA, (uint256));  
        sizeB = abi.decode(dataB, (uint256));  
    }  
}  
  
// codesize 174  
contract CalledA {  
    function getCodeSize() public pure returns (uint size) {  
        assembly {  
            size := codesize()  
        }  
    }  
}  
  
// codesize 180  
contract CalledB {  
    function getCodeSize() public pure returns (uint size) {  
        uint unused = 100;  
        assembly {  
            size := codesize()  
        }  
    }  
}  
  
// 你可以使用这个合约来检查合约的大小  
contract MeasureContractSize {  
    function measureConctract(address c) external view returns (uint256 size){  
        size = c.code.length;  
    }  
}
```如果 `codesize` 函数返回的是 `Caller` 合约的大小，那么通过委托调用 `ContractA` 和 `ContractB` 从 `getSizes()` 返回的值将是相同的。也就是说，它们将是 `Caller` 的大小，即 1103。然而，如下图所示，返回的值是不同的，这明确表明这些是 `CalledA` 和 `CalledB` 的大小。

![](https://img.learnblockchain.cn/attachments/migrate/1721878510778)

# 委托调用的委托调用

有人可能会问：如果一个合约发出 `**delegatecall**` 给第二个合约，而第二个合约又发出 `**delegatecall**` 给第三个合约，会发生什么？在这种情况下，上下文将保持为发起第一个 `**delegatecall**` 的合约，而不是中间的合约。

其工作原理如下：

* `Caller` 合约委托调用 `CalledFirst` 合约中的 `logSender()` 函数。
* 该函数旨在发出一个事件，记录 `**msg.sender**`。
* 此外，`CalledFirst` 合约除了创建这个日志外，还委托调用 `CalledLast` 合约。
* `CalledLast` 合约也会发出一个事件，同样记录 `**msg.sender**`。

下面是描述此流程的图表。

![](https://img.learnblockchain.cn/attachments/migrate/1721878511454)

请记住，所有的委托调用只是借用了被委托调用合约的字节码。可以这样想象，这些字节码暂时被“吸收”到调用合约中。当我们这样看时，我们会发现 msg.sender 始终是原始的 msg.sender，因为所有事情都发生在 Caller 内部。请参见下面的动画：

下面我们提供一些源代码来测试委托调用的委托调用的概念：

```solidity
contract Caller {  
    address calledFirst = 0xF27374C91BF602603AC5C9DaCC19BE431E3501cb;  
    function delegateCallToFirst() public {  
        calledFirst.delegatecall(  
			abi.encodeWithSignature("logSender()")  
		);  
    }  
}  
  
contract CalledFirst {  
    event SenderAtCalledFirst(address sender);  
    address constant calledLast = 0x1d142a62E2e98474093545D4A3A0f7DB9503B8BD;  
    function logSender() public {  
        emit SenderAtCalledFirst(msg.sender);  
        calledLast.delegatecall(  
			abi.encodeWithSignature("logSender()")  
		);  
    }  
}  
  
contract CalledLast {  
    event SenderAtCalledLast(address sender);  
    function logSender() public {  
        emit SenderAtCalledLast(msg.sender);  
    }  
}
```

我们可能会认为 `CalledLast` 中的 `**msg.sender**` 将是 `CalledFirst` 的地址，因为它是调用 `CalledLast` 的合约，但这不符合我们的模型，即通过 `delegatecall` 调用的合约的字节码只是被借用，而上下文始终是执行 `delegatecall` 的合约。

最终结果是，两个 `**msg.sender**` 值都对应于发起 `Caller.delegateCallToFirst()` 交易的账户。这可以在下图中观察到，我们在 Remix 中执行此过程并捕获日志。

![](https://img.learnblockchain.cn/attachments/migrate/1721878511655)

msg.sender 在 CalledFirst 和 CalledLast 中是相同的

一个混淆的来源是，有人可能会描述这个操作为“`Caller` 委托调用 `CalledFirst`，而 `CalledFirst` 委托调用 `CalledLast`。”但这听起来像是 `CalledFirst` 在进行委托调用——事实并非如此。`CalledFirst` 提供字节码给 `Called`——而该字节码从 `Called` 发出委托调用给 `CalledLast`。

从委托调用中调用

让我们引入一个情节转折并修改 CalledFirst 合约。现在，CalledFirst 将使用 `**call**` 而不是 `**delegatecall**` 调用 CalledLast。

![](https://img.learnblockchain.cn/attachments/migrate/1721878511767)

换句话说，CalledFirst 合约需要更新为以下代码：

```solidity
contract CalledFirst {  
    event SenderAtCalledFirst(address sender);  
    address constant calledLast = ...;  
  
    function logSender() public {  
        emit SenderAtCalledFirst(msg.sender);  
        calledLast.call(  
			abi.encodeWithSignature("logSender()")  
		); // this is new  
    }  
}
```

问题来了：在 `SenderAtCalledLast` 事件中记录的 `**msg.sender**` 会是什么？以下动画说明了会发生什么：

![](https://img.learnblockchain.cn/attachments/migrate/1721878511882)

当 `Caller` 通过 `**delegatecall**` 调用 `CalledFirst` 中的一个函数时，该函数在 `Caller` 的上下文中执行。请记住，`CalledFirst` 只是“借出”其字节码供 `Caller` 执行。此时，就像我们在 `Caller` 合约中执行 `**msg.sender**`，这意味着 msg.sender 是发起交易的地址。

![](https://img.learnblockchain.cn/attachments/migrate/1721878512474)

现在，`CalledFirst` 调用 `CalledLast`，但 `CalledFirst` 在 `Caller` 的上下文中使用，所以就像 `Caller` 调用了 `CalledLast`。在这种情况下，`CalledLast` 中的 `**msg.sender**` 将是 `Caller` 的地址。

在下图中，我们观察到 Remix 中的日志。请注意，这次 `**msg.sender**` 值是不同的。

![](https://img.learnblockchain.cn/attachments/migrate/1721878512649)

CalledLast 中的 msg.sender 是 Caller 的地址

**练习：** 如果 Caller _调用_ CalledFirst 而 CalledFirst _委托调用_ CalledLast，并且每个合约记录 msg.sender，那么每个合约将记录哪个消息发送者？

# 低级委托调用

在本节中，我们将使用 **YUL** 中的 `**delegatecall**` 来深入探索其功能。**YUL** 中的函数与操作码语法非常相似，因此首先查看 `**DELEGATECALL**` 操作码的定义是有益的。

`**DELEGATECALL**` 从堆栈中获取 6 个参数，依次为：**gas**、**address**、**argsOffset**、**argsSize**、**retOffset** 和 **retSize**，并返回一个值到堆栈，指示操作是否成功（1）或不成功（0）。

每个参数的解释如下（取自 [evm.codes](http://evm.codes/)）：

1. **gas**：发送到子上下文执行的 gas 数量。未被子上下文使用的 gas 将返回到此上下文。
2. **address**：要执行其代码的账户。
3. **argsOffset**：内存中的字节偏移量，以字节为单位，子上下文的 calldata。
4. **argsSize**：要复制的字节大小（calldata 的大小）。
5. **retOffset**：内存中的字节偏移量，以字节为单位，存储子上下文的返回数据的位置。
6. **retSize**：要复制的字节大小（返回数���的大小）。

使用委托调用向合约发送以太币是不允许的（想象一下如果允许的话可能的漏洞！）。另一方面，`CALL` 操作码允许以太币转移，并包含一个额外的参数来指示应发送多少以太币。

在 YUL 中，`**delegatecall**` 函数与 `**DELEGATECALL**` 操作码相似，并包含上述相同的 6 个参数。其语法为：

`delegatecall(g, a, in, insize, out, outsize).`

下面，我们展示一个包含两个执行相同操作的函数的合约，执行一个 `**delegatecall**`。一个是用纯 Solidity 编写的，另一个包含 YUL。

```solidity
contract DelegateYUL {  
  
    function delegateInSolidity(        address _address    ) public returns (bytes memory data) {  
        (, data) = _address.delegatecall(  
			abi.encodeWithSignature("sayOne()")  
		);  
    }  
``````  
    function delegateInYUL(		address \_address	) public returns (uint data) {  
        assembly {  
            mstore(0x00, 0x34ee2172) // Load the calldata I intend to send into memory at 0x00. The first slot will become 0x0000000000000000000000000000000000000000000000000000000034ee2172  
            let result := delegatecall(gas(), \_address, 0x1c, 4, 0, 0x20) // The third parameter indicates the starting position in memory where the calldata is located, the fourth parameter specifies its size in bytes, and the fifth parameter specifies where the returned calldata, if any, should be stored in memory  
            data := mload(0) // Read delegatecall return from memory  
        }  
    }  
}  
  
contract Called {  
    function sayOne() public pure returns (uint) {  
        return 1;  
    }  
}

In the `delegateInSolidity` function, I utilize the `**delegatecall**` method in Solidity, passing as a parameter the signature of the `sayOne` function, calculated using the `abi.encodeWithSignature` method.

If we don’t know the size of the return in advance, don’t worry, we can use the returndatacopy function later to handle this. In another article, when we delve deeper into writing upgradable contracts using delegatecall, we will cover all these details.

# EIP 150 and gas forwarded

A note on an issue regarding forwarded gas: We utilize the `gas()` function as the first parameter of `**delegatecall**`, which returns the available gas. This should indicate that we intend to forward all available gas. However, since the **Tangerine Whistle fork**, there has been a [cap of 63/64 of the total possible gas](https://www.rareskills.io/post/eip-150-and-the-63-64-rule-for-gas) for forwarding via `**delegatecall**` (and other opcodes). In other words, although the `gas()` function returns all available gas, only 63/64 of it is forwarded to the new subcontext, while 1/64 is retained.

# Conclusion

To conclude this article, let’s summarize what we’ve learned. `Delegatecall` allows for the execution of functions defined in other contracts within the context of the calling contract. The called contract, also known as the implementation contract, merely provides its bytecode, and nothing within it is changed or fetched from its storage.

`Delegatecall` is employed to separate the contract where the data is stored from the contract where the business logic or function implementation is housed. **This forms the foundation of the most used pattern of contract upgradability in Solidity.** However, as we have observed, `delegatecall` must be utilized with great care, as unintentional changes to state variables can occur, potentially rendering the calling contract unusable.

# Learn More with RareSkills

For those new to Solidity, see our free [Solidity course](https://www.rareskills.io/learn-solidity). Intermediate Solidity developers please see our [Solidity Bootcamp](https://www.rareskills.io/solidity-bootcamp).

# Authorship

This article was written by [João Paulo Morais](https://www.linkedin.com/in/jpmorais/) in collaboration with RareSkills.
```

在 `delegateInSolidity` 函数中，我使用了 Solidity 中的 `**delegatecall**` 方法，作为参数传递了通过 `abi.encodeWithSignature` 方法计算的 `sayOne` 函数的签名。

如果我们事先不知道返回值的大小，不用担心，我们可以稍后使用 returndatacopy 函数来处理。在另一篇文章中，当我们深入探讨使用 delegatecall 编写可升级合约时，我们将涵盖所有这些细节。

# EIP 150 和 gas 转发

关于转发 gas 的问题说明：我们使用 `gas()` 函数作为 `**delegatecall**` 的第一个参数，它返回可用的 gas。这应该表明我们打算转发所有可用的 gas。然而，自从 **Tangerine Whistle 分叉**以来，通过 `**delegatecall**`（和其他操作码）转发的 gas 总量被限制为 [总可能 gas 的 63/64](https://www.rareskills.io/post/eip-150-and-the-63-64-rule-for-gas)。换句话说，尽管 `gas()` 函数返回所有可用的 gas，但只有 63/64 被转发到新的子上下文，而 1/64 被保留。

# 结论

总结本文，我们学到了什么。`Delegatecall` 允许在调用合约的上下文中执行其他合约中定义的函数。被调用的合约，也称为实现合约，仅提供其字节码，其存储中的任何内容都不会被更改或获取。

`Delegatecall` 被用来将存储数据的合约与存放业务逻辑或函数实现的合约分开。**这是 Solidity 中最常用的合约可升级模式的基础。**然而，正如我们所看到的，`delegatecall` 必须非常谨慎地使用，因为可能会发生对状态变量的意外更改，可能会导致调用合约无法使用。

# 了解更多 RareSkills

对于 Solidity 新手，请参阅我们的免费 [Solidity 课程](https://www.rareskills.io/learn-solidity)。中级 Solidity 开发人员请参阅我们的 [Solidity Bootcamp](https://www.rareskills.io/solidity-bootcamp)。

# 作者

本文由 [João Paulo Morais](https://www.linkedin.com/in/jpmorais/) 与 RareSkills 合作撰写。

> 我是 [AI 翻译官](https://learnblockchain.cn/people/19584)，为大家转译优秀英文文章，如有翻译不通的地方，在[这里](https://github.com/lbc-team/Pioneer/blob/main/translations/8827.md)修改，还请包涵～